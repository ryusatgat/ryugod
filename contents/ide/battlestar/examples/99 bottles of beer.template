//
//  Battlestar: 99 bottles of beer
//  https://github.com/xyproto/battlestar
//

const bottle = " bottle"
const plural = "s"
const ofbeer = " of beer"
const wall = " on the wall"
const sep = ", "
const takedown = "Take one down and pass it around, "
const u_no = "No"
const l_no = "no"
const more = " more bottles of beer"
const store = "Go to the store and buy some more, "
const dotnl = ".\n"
const nl = "\n"

// Write two digits, based on the value in ax
fun printnum
    bx = ax
    loop
        break (ax < 10)
        ax /= 10
        // modulo in dx after idiv
        bx = dx
        ax += 48 // ASCII value for '0'
        print(chr(ax))
        break
    end
    ax = bx
    ax += 48 // ASCII value for '0'
    print(chr(ax))
end

// Write an "s" if ax is 1
fun pluralifneeded
    loop
        break (ax == 1)
        print(plural)
        break
    end
end

fun main
    loop 99
        // Save loop counter for later, twice
        cx -> stack
        cx -> stack

        // Print the loop counter (passed by ax)
        ax = cx
        printnum()

        // N, "bottles of beer on the wall, "
        print(bottle)
        print(plural)
        print(ofbeer)
        print(wall)
        print(sep)

        // Retrieve and print the number
        stack -> ax
        printnum()

        // N, "bottles of beer."
        print(bottle)
        print(plural)
        print(ofbeer)
        print(dotnl)

        // "Take one down and pass it around,"
        print(takedown)

        // N-1, "bottles of beer on the wall."
        stack -> ax
        ax--
        // Store N-1, used just a few lines down
        ax -> stack
        printnum()
        print(bottle)
        // Retrieve N-1
        stack -> ax
        pluralifneeded()
        print(ofbeer)
        print(wall)
        print(dotnl)

        // Blank line
        print(nl)

        // Skip to the top of the loop while the counter is >= 2
        continue (cx >= 2)

        // At the last two

        // "1 bottle of beer on the wall,"
        ax = 1
        printnum()
        print(bottle)
        print(ofbeer)
        print(wall)
        print(sep)

        // "1 bottle of beer."
        ax = 1
        printnum()
        print(bottle)
        print(ofbeer)
        print(dotnl)

        // "Take one down and pass it around,"
        print(takedown)

        // "no more bottles of beer on the wall."
        print(l_no)
        print(more)
        print(wall)
        print(dotnl)

        // Blank line
        print(nl)

        // "No more bottles of beer on the wall,"
        print(u_no)
        print(more)
        print(wall)
        print(sep)

        // "no more bottles of beer."
        print(l_no)
        print(more)
        print(dotnl)

        // "Go to the store and buy some more,"
        print(store)

        // "99 bottles of beer on the wall."
        ax = 99
        printnum()
        print(bottle)
        print(plural)
        print(ofbeer)
        print(wall)
        print(dotnl)
    end
end