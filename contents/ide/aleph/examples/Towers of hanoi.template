$
$   Aleph: Towers of hanoi
$   https://github.com/lcsirmaz/aleph
$

'charfile'print="<<stdout>>">.
'stack'[1]a,[1]b,[1]c.
'constant'size=5.
'action'move tower+>length+[]from[]+[]via[]+[]to[]:
   length=0;
   decr+length,move tower+length+from+to+via,
     move disc+from+to,print towers,
     move tower+length+via+from+to.
'action'move disc+[]st1[]+[]st2[]:
   (* st1[>>st1]-> st2 *)st2, unstack+st1.
'action'print towers-ln:
   size->ln,
  (lines:
     ln=0;
     print disc+a+ln,printdisc+b+ln,
      print disc+c+ln,put char+print+new line,
      decr+ln,:lines).
'action'print disc+[]st[]+>line-index:
   subtr+line+1+index,add+index+<<st+index,
     (was+st+index,print actual disc+st[index];
    print blank disc).
'action'print actual disc+>nmb-spc:
   subtr+size+nmb+spc,
     repeat+spc+/ /,repeat+nmb+/*/,repeat+1+/*/,
     repeat+nmb+/*/,repeat+spc+/ /.
'action'print blank disc:
   repeat+size+/ /,repeat+1+/ /,repeat+size+/ /.
'action'repeat+>cnt+>sb:
   cnt=0; put char+print+sb,decr+cnt,:repeat.
'action'play towers-n:
   size->n,
     (fill a: n=0; decr+n, * n->a *a, :fill a),
     print towers,move tower+size+a+b+c.
'root'play towers.
'end'
