//
//  Slogan: regular expression
//  https://github.com/vijaymathew/slogan
//

// search for regexp anywhere in text
function regex_match(regexp, text)
    if (regexp[0] == \^)
        matchhere(regexp[1:], text)
    else if (matchhere(regexp, text)) true
    else if (string_length(text) > 0)
        regex_match(regexp, text[1:])
    else false

// search for regexp at beginning of text
function matchhere(regexp, text)
    if (string_length(regexp) == 0) true
    else if (string_length(regexp) > 1 && regexp[1] == \*)
        matchstar(regexp[0], regexp[2:], text)
    else if (regexp[0] == \$ && string_length(regexp) == 1)
        string_length(text) == 0
    else if (string_length(text) > 0 && (regexp[0]==\. || regexp[0]==text[0]))
        matchhere(regexp[1:], text[1:])
    else false

// search for c*regexp at beginning of text
function matchstar(c, regexp, text)
    if (string_length(text) == 0) false
    else if (matchhere(regexp, text)) true
    else if (text[0] == c || c == \.)
        matchhere(regexp, text[1:])
    else false

assert regex_match("^bat", "battering ram")
assert not(regex_match("^bat", "cricket bat"))
assert regex_match("xyz$", "abcxyz")
assert not(regex_match("xyz$", "abcxyzz"))
assert not(regex_match("abc", "ababab"))
assert regex_match("ab*", "ababab")